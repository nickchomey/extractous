plugins {
    id 'java-library'
    id 'org.graalvm.buildtools.native' version '0.10.4' // Updated for GraalVM 25 and Gradle 9 compatibility
}


def tikaVersion = "3.2.3"
def numThreads = Runtime.getRuntime().availableProcessors()
def currentOs = org.gradle.internal.os.OperatingSystem.current()


def osName = ""
if (currentOs.isLinux()) {
    osName = "linux"
} else if (currentOs.isMacOsX()) {
    osName = "macos"
} else if (currentOs.isWindows()) {
    osName = "windows"
}

group = 'ai.yobix'
version = "tika-$tikaVersion-$osName"


repositories {
    mavenCentral()
}

dependencies {
    // Tika uses slf4j, just use a nop logger to ignore all logging
    implementation("org.slf4j:slf4j-nop:2.0.16")
    // Some dependencies use log4j such as poi, route log4j back to slf4j
    // Using latest 2.x version (3.x doesn't exist yet as of Oct 2025)
    implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.24.2'

    // Jakarta Mail API - required for email parsing and GraalVM native-image
    implementation 'jakarta.mail:jakarta.mail-api:2.1.3'
    implementation 'org.eclipse.angus:angus-mail:2.0.3'  // Jakarta Mail implementation

    implementation("org.apache.tika:tika-core:$tikaVersion")
    implementation "org.apache.tika:tika-parsers-standard-package:$tikaVersion" // Apache Tika parsers

    // Core parser modules (comprehensive coverage for all formats)
    implementation("org.apache.tika:tika-parser-microsoft-module:$tikaVersion")      // Office (DOCX, XLSX, PPTX, DOC, XLS, PPT)
    implementation("org.apache.tika:tika-parser-pdf-module:$tikaVersion")             // PDF (fallback)
    implementation("org.apache.tika:tika-parser-html-module:$tikaVersion")            // HTML, XHTML
    implementation("org.apache.tika:tika-parser-apple-module:$tikaVersion")           // iWork (Pages, Numbers, Keynote)
    implementation("org.apache.tika:tika-parser-audiovideo-module:$tikaVersion")      // Media metadata
    implementation("org.apache.tika:tika-parser-cad-module:$tikaVersion")             // CAD (AutoCAD, SolidWorks) - UNUSUAL GOLD
    implementation("org.apache.tika:tika-parser-crypto-module:$tikaVersion")          // Encrypted/password-protected - PRIORITY
    implementation("org.apache.tika:tika-parser-font-module:$tikaVersion")            // Font metadata
    implementation("org.apache.tika:tika-parser-mail-module:$tikaVersion")            // Email (EML, MSG, PST, MBOX)
    implementation("org.apache.tika:tika-parser-miscoffice-module:$tikaVersion")      // Legacy (WordPerfect, Lotus, dBase) - LEGACY GOLD
    implementation("org.apache.tika:tika-parser-news-module:$tikaVersion")            // RSS, Atom
    implementation("org.apache.tika:tika-parser-ocr-module:$tikaVersion")             // Tesseract OCR integration
    implementation("org.apache.tika:tika-parser-pkg-module:$tikaVersion")             // Archives (ZIP, TAR, 7Z, RAR, etc.)
    implementation("org.apache.tika:tika-parser-text-module:$tikaVersion")            // Text variants
    implementation("org.apache.tika:tika-parser-xml-module:$tikaVersion")             // XML variants
    implementation("org.apache.tika:tika-parser-webarchive-module:$tikaVersion")      // WARC web archives

    // Additional modules for comprehensive coverage
    implementation("org.apache.tika:tika-parser-code-module:$tikaVersion")            // Source code files
    implementation("org.apache.tika:tika-parser-advancedmedia-module:$tikaVersion")   // Advanced media formats
}

tasks.register('runAgent', JavaExec) {
    dependsOn classes                      // ensure byte-code is built
    group = 'verification'
    description = 'Runs Tika with the native-image agent to generate GraalVM configs'

    mainClass = 'ai.yobix.AgentRunner'
    classpath = sourceSets.main.runtimeClasspath

    // --- file you want to analyse -----
    // If a file fails to parse, we should pass it here to generate metadata for it,
    // then merge that metadata into existing.
    args '../../test_files/documents/vodafone.xlsx'

    // --- GraalVM agent  ---------------
    jvmArgs "-agentlib:native-image-agent=config-output-dir=graal-agent"
}

graalvmNative {
    // The merge option didn't work as expected so currently not using it
    // It should work by running `.gradlew -Pagent runAgent` which should generate metadata
    // in the `graal-agent` directory. We should then be able to merge that metadata into the
    // `src/main/resources/META-INF/$group/$version` directory, but the merge didn't include the new metadata.
    // Likely user error.
    // agent {
    //     defaultMode = "standard"
    //     metadataCopy {
    //         mergeWithExisting = true
    //         inputTaskNames.add("runAgent")
    //         outputDirectories.add("src/main/resources/META-INF/$group/$version")
    //     }
    // }
    metadataRepository {
        enabled = true
    }
    // Make sure this is off to use the same GraalVM version thant the one used by gradle
    // Setting the version is left outside of this script in github workflow or developer build environment
    toolchainDetection = false

    // Set to false to disable to run the tests in native mode.
    testSupport = true

    binaries {
        main {
            imageName = 'libtika_native'

            // To build a shared lib, make sure to not set the mainClass
            sharedLibrary = true

            // The --no-fallback option to native-image causes the utility to fail if it can not create the image.
            fallback = false

            configurationFileDirectories.from(file("src/main/resources/META-INF/$group/$version"))

            buildArgs.addAll(
                    "-H:+AddAllCharsets", // Very important to get UTF8 working
                    "--enable-https", // Very important https working
                    "-O3",
                    "--parallelism=$numThreads",
                    "-march=compatibility", // VERY IMPORTANT to use compatibility flag. If not the libs will use the cpu arch of the build machine and will notwork on other CPUs if distributed

                    // GraalVM 25 optimizations
                    // Note: --strict-image-heap is now default in GraalVM 25
                    "-H:+UnlockExperimentalVMOptions",
                    "-H:+RemoveUnusedSymbols",            // Smaller binary
                    "-H:+ReportExceptionStackTraces"      // Better error messages
            )
            jvmArgs.add('-Djava.awt.headless=true')
            requiredVersion = '25' // Upgraded from 23 for latest GraalVM optimizations
        }
    }
}
